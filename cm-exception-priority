echo start expections configuration print\n
echo \nsystem handler priority register\n
printf "-- %d(0x%x) mem fault\n",     *0xE000ED18 & 0x000000FF,           *0xE000ED18 & 0x000000FF
printf "-- %d(0x%x) bus fault\n",     ( *0xE000ED18 & 0x0000FF00 ) >> 8,  ( *0xE000ED18 & 0x0000FF00 ) >> 8
printf "-- %d(0x%x) usage fault\n",   ( *0xE000ED18 & 0x00FF0000 ) >> 16, ( *0xE000ED18 & 0x00FF0000 ) >> 16
printf "-- %d(0x%x) secure fault\n",  ( *0xE000ED18 & 0xFF000000 ) >> 24, ( *0xE000ED18 & 0xFF000000 ) >> 24
printf "-- %d(0x%x) svc fault\n",     ( *0xE000ED1C & 0xFF000000 ) >> 24, ( *0xE000ED1C & 0xFF000000 ) >> 24
printf "-- %d(0x%x) debug monitor\n", *0xE000ED20 & 0x000000FF,           *0xE000ED20 & 0x000000FF
printf "-- %d(0x%x) pend sv\n",       ( *0xE000ED20 & 0x00FF0000 ) >> 16, ( *0xE000ED20 & 0x00FF0000 ) >> 16
printf "-- %d(0x%x) sys tick\n",      ( *0xE000ED20 & 0xFF000000 ) >> 24, ( *0xE000ED20 & 0xFF000000 ) >> 24

echo \ninterrupt priority register\n
set $interrupt_index = 0
while ($interrupt_index < 124)
  set $int0_priority = ( *( 0xE000E400 + 4 * $interrupt_index ) ) & 0x000000FF
  set $int1_priority = ( ( *( 0xE000E400 + 4 * $interrupt_index ) ) & 0x0000FF00 ) >> 8
  set $int2_priority = ( ( *( 0xE000E400 + 4 * $interrupt_index ) ) & 0x00FF0000 ) >> 16
  set $int3_priority = ( ( *( 0xE000E400 + 4 * $interrupt_index ) ) & 0xFF000000 ) >> 24

  printf "%3d(0x%-2x) int#%3d(0x%-3x) ",   $int0_priority, $int0_priority, 4 * $interrupt_index + 0, 4 * $interrupt_index + 0
  printf "| %3d(0x%-2x) int#%3d(0x%-3x) ", $int1_priority, $int1_priority, 4 * $interrupt_index + 1, 4 * $interrupt_index + 1
  printf "| %3d(0x%-2x) int#%3d(0x%-3x) ", $int2_priority, $int2_priority, 4 * $interrupt_index + 2, 4 * $interrupt_index + 2
  printf "| %3d(0x%-2x) int#%3d(0x%-3x) ", $int3_priority, $int3_priority, 4 * $interrupt_index + 3, 4 * $interrupt_index + 3
  printf "\n"

  set $interrupt_index = $interrupt_index + 1
end
echo end expections configuration print\n
